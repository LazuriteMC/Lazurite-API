plugins {
    id 'com.github.johnrengelman.shadow' version "$shadow_version"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$fabric_loader_version"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
    common(project(path: ":common", configuration: "namedElements"))
    common("com.github.stephengold:Libbulletjme:$libbulletjme_version")
    modApi include("dev.lazurite:toolbox-fabric:$toolbox_version")
    modApi include("dev.lazurite:transporter-fabric:$transporter_version")

    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
    shadowCommon("com.github.stephengold:Libbulletjme:$libbulletjme_version") { transitive false }

}

remapJar {
    dependsOn shadowJar
    input.set shadowJar.archiveFile
}

tasks.withType(ProcessResources) {
    inputs.property "version", version

    filesMatching "fabric.mod.json", {
        expand "version": version
    }
}

sourcesJar {
    from project(":common").sourceSets.main.allSource
}
publishing {
    publications {
        fabric(MavenPublication) {
            artifactId = archivesBaseName
            from components.java
        }
    }
}