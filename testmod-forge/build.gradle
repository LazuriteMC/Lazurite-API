architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    forge {
        mixinConfig "rayon.mixins.json", "toolbox.mixins.json"

        localMods {
            forge {
                it.add(project(":forge").sourceSets.main)
            }
        }
    }
}

configurations {
    common
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

tasks.withType(ProcessResources) {
    inputs.property "version", version

    filesMatching "META-INF/mods.toml", {
        expand "version": version
    }
}

dependencies {
    forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    implementation(project(path: ":forge", configuration: "namedElements"))
    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    common(project(path: ':testmod-common', configuration: 'namedElements')) { transitive false }

    common("dev.lazurite:toolbox-forge:$toolbox_version") { transitive false }
    common("dev.lazurite:transporter-forge:$transporter_version") { transitive false }
    common("com.github.stephengold:Libbulletjme:$libbulletjme_version") { transitive false }
}