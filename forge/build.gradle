plugins {
    id 'com.github.johnrengelman.shadow' version "$shadow_version"
}

loom.forge.mixinConfig 'rayon.mixins.json', 'toolbox.mixins.json'

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:$minecraft_version-$forge_version"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    common("com.github.stephengold:Libbulletjme:$libbulletjme_version") { transitive false }
    common("dev.lazurite:toolbox-forge:$toolbox_version") { transitive false }
    common("dev.lazurite:transporter-forge:$transporter_version") { transitive false }

    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive false }
    shadowCommon("com.github.stephengold:Libbulletjme:$libbulletjme_version") { transitive false }
    shadowCommon("dev.lazurite:toolbox-forge:$toolbox_version") { transitive false }
    shadowCommon("dev.lazurite:transporter-forge:$transporter_version") { transitive false }
}

tasks.withType(ProcessResources) {
    inputs.property "version", version

    filesMatching "META-INF/mods.toml", {
        expand "version": version
    }
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}