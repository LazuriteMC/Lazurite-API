plugins {
    id 'architectury-plugin' version "$architectury_plugin_version"
    id 'com.github.johnrengelman.shadow' version "$shadow_version"
    id 'dev.architectury.loom' version "$loom_version" apply false
}

allprojects {
    apply plugin: 'java'

    version rayon_version
    group maven_group

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }

        withSourcesJar()
    }
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    archivesBaseName = rootProject.archivesBaseName + '-' + name
    loom.silentMojangMappingsLicense()

    repositories {
        maven {
            name 'Lazurite'
            url 'https://lazurite.dev/releases'
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:$minecraft_version"
        mappings loom.officialMojangMappings()
        api "com.github.stephengold:Libbulletjme:$libbulletjme_version"
    }

    publishing {
        repositories {
            maven {
                name 'Lazurite'
                url 'https://lazurite.dev/releases'

                credentials {
                    username property('publish.lazurite.alias')
                    password property('publish.lazurite.secret')
                }
            }
        }
    }
}