plugins {
	id 'com.github.johnrengelman.shadow' version "$shadow_version" apply false
	id 'fabric-loom' version "$loom_version" apply false
}

allprojects {
	apply plugin: 'com.github.johnrengelman.shadow'
	apply plugin: 'fabric-loom'

	version mod_version
	group maven_group
	jar.from 'LICENSE'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(16)
		}

		withSourcesJar()
	}

	processResources {
		inputs.property 'version', version

		filesMatching('fabric.mod.json') {
			expand 'version': version
		}
	}

	repositories {
		maven { url 'https://lazurite.dev/releases' }
	}

	dependencies {
		mappings loom.officialMojangMappings()
		minecraft "com.mojang:minecraft:$minecraft_version"
		modImplementation "net.fabricmc:fabric-loader:$loader_version"
	}
}

subprojects {
	apply plugin: 'maven-publish'

	dependencies {
		modImplementation fabricApi.module('fabric-api-base', fabric_version)
		modImplementation fabricApi.module('fabric-rendering-v1', fabric_version)
		modImplementation fabricApi.module('fabric-networking-api-v1', fabric_version)
		modImplementation fabricApi.module('fabric-lifecycle-events-v1', fabric_version)
		modImplementation fabricApi.module('fabric-object-builder-api-v1', fabric_version)

		api "com.github.stephengold:Libbulletjme:$libbulletjme_version"
		modApi "dev.lazurite:transporter:$transporter_version"
		modApi "dev.lazurite:toolbox:$toolbox_version"
	}

	publishing {
		publications {
			rayon(MavenPublication) {
				artifact(remapJar)

				artifact(sourcesJar) {
					builtBy remapSourcesJar
				}
			}
		}

		repositories {
			maven {
				name 'Lazurite'
				url 'https://lazurite.dev/maven/releases'

				credentials {
					username = property('publish.lazurite.alias')
					password = property('publish.lazurite.secret')
				}
			}
		}
	}
}

dependencies {
	include project(':rayon-core')
	include project(':rayon-entity')
	include project(':rayon-particle')
}